apply plugin: 'com.android.library'

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 4
        targetSdkVersion 33

        externalNativeBuild {
            cmake {
                cppFlags ''
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'jackpal.androidterm.libtermexec'
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
}

// by default recent plugin version does not copy any AIDL files "to avoid publishing too much"
android.libraryVariants.all { variant ->
    Sync packageAidl = project.tasks.create("addPublic${variant.name.capitalize()}Aidl", Sync) { sync ->
        from "$project.projectDir/src/main/aidl/"
        into "$buildDir/intermediates/bundles/${variant.dirName}/aidl/"
    }

    variant.javaCompile.dependsOn packageAidl
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.annotation:annotation:1.5.0'
}
